plugins {
    id "de.undercouch.download" version "3.3.0"
}

apply plugin: 'cpp' 
apply plugin: 'google-test'
apply from: 'gradle/setup-googletest.gradle'

// PATH Variables
def external = "$projectDir/3rdparty"

model {
    repositories {
        libs(PrebuiltLibraries) { 
            googleTest { 
                headers.srcDir new File(external, "/googletest-master/googletest/include") 
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("$external/googletest-master/googletest/build/" + findGoogleTestStaticLib(targetPlatform)) 
                }
            }
        }
    }
    components {
        sealib(NativeLibrarySpec) { } 
        // For later visualization
       /* main(NativeExecutableSpec) { 
            sources {
                cpp.lib library: "sealib" 
            }
        }*/
    }

    binaries {
        withType(SharedLibraryBinarySpec) {
            if (toolChain in VisualCpp) {
                cppCompiler.define "DLL_EXPORT" 
            }
        }
        withType(GoogleTestTestSuiteBinarySpec) {
            lib library: "googleTest", linkage: "static" 

            if (targetPlatform.operatingSystem.linux) {
                cppCompiler.args '-pthread'
                linker.args '-pthread'
            }
        }
    }
}

def findGoogleTestStaticLib(Platform p) {
    if (p.operatingSystem.windows) {
        return "Release/gtest.lib"
    } else {
        return "libgtest.a"
    }
}

clean.doFirst {
    delete external
}